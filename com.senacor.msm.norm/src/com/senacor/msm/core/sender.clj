(ns com.senacor.msm.core.sender
  (:require [com.senacor.msm.core.norm-api :as norm]
            [com.senacor.msm.core.control :as c]
            [com.senacor.msm.core.monitor :as mon]
            [com.senacor.msm.core.util :as util]
            [clojure.core.async :refer [chan close! go-loop sliding-buffer tap untap <! <!! >!! >! poll!]]
            [clojure.tools.logging :as log]))

(def ^:const buffer-size (* 1024 1024))

;;
;; Send messages across a norm stream
;;

(defn wait-for-events
  "Pseudo-blocks waiting for one or more events.
  chan is the event channel where the events are received.
  session is the session to which the events related.
  event-types is a set of event-type keys the wait is expecting to receive.
  Returns the first event that matched the condition"
  [chan session event-types]
  (log/trace "Wait for events" event-types)
  (loop [m (<!! chan)]
    (if (and m
             (not (and (contains? event-types (:event-type m))
                       (= session (:session m)))))
      (recur (<!! chan))
      (do
        (log/trace "Wait ends with" m)
        m))))

(defn command-handler
  "Receive commands as byte array messages from cmd-chan and send them
  to all NORM receivers of this session. Use event-chan to receive
  confirmation that messages have been sent."
  [session event-chan cmd-chan]
  (let [ec-tap (chan (sliding-buffer 5))]
    (go-loop [cmd (<! cmd-chan)]
      (when cmd
        (tap event-chan ec-tap)
        (norm/send-command session cmd (count cmd) true)
        (wait-for-events ec-tap session #{:tx-cmd-sent})
        (untap event-chan ec-tap)
        (recur (<! cmd-chan)))))
  cmd-chan)

(defn stop-sender
  [session stream event-chan]
  (let [ec-tap (tap event-chan (chan 5))]
    (log/trace "closing session")
    (norm/flush-stream stream true :passive)
    (norm/close-stream stream true)
    (Thread/sleep 1000)
    (norm/stop-sender session)
    (norm/destroy-session session)
    (mon/unregister session)
    (log/trace "session and stream closed")
    (untap event-chan ec-tap)))

(defn create-sender
  "Creates a message sender
  session NORM session used to communicate externally
  instance-id numeric identifier of the sender, used to distinguish
    senders in the NORM universe
  event-chan is an inbound channel of NORM control events generated by
    the main event loop.
  cmd-chan is an inbound channel with NORM commands sent to all
    communication partners.
  in-chan An inbound channel with byte-arrays of messages to be sent.
  max-msg-size in bytes, used to appropriately size the NORM buffers."
  [session instance-id event-chan in-chan cmd-chan max-msg-size]
  (norm/start-sender session instance-id buffer-size max-msg-size 64 16)
  (let [stream (norm/open-stream session buffer-size)
        ec-tap (chan 10 (filter #(contains? #{:tx-queue-vacancy :tx-queue-empty} (:event-type %))))]
    (command-handler session event-chan cmd-chan)
    (tap event-chan ec-tap)
    (log/trace "Sender registered, starting loop")
    (go-loop [b-arr (<! in-chan)
              b-len (count b-arr)
              b-offs 0]
      (if b-arr
        (do
          (log/tracef "stream write len=%d, offs=%d" b-len b-offs)
          (let [bytes-sent (norm/write-stream stream b-arr b-offs b-len)]
            (if (= bytes-sent b-len)
              (let [n-arr (<! in-chan)]
                (recur n-arr (count n-arr) 0))
              (do
                (log/tracef "wait free out buffer space, remaining=%d" (- b-len bytes-sent))
                (wait-for-events ec-tap session #{:tx-queue-empty :tx-queue-vacancy})
                (log/trace "buffer space available")
                (recur b-arr (- b-len bytes-sent) (+ b-offs bytes-sent)))))
          )
        (stop-sender session stream event-chan)
        ))))
